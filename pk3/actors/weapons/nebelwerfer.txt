actor NebRocket
{
	scale 0.5
	Radius 11
	Height 8
	Speed 35
	Damage 8
	Projectile
	+RANDOMIZE
	+FORCERADIUSDMG
	DeathSound "weapons/nebxpl"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	MissileHeight 0
	Decal Scorch
	States
	{
	Spawn:
		MNSS A 1 bright A_CustomMissile("RocketFlameSpawner",0,0,0,5)
		loop
	Death:
		FRME A 1 bright A_Explode(64,56)
		FRME BCDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

actor NebRocketEnemy : NebRocket
{
	Speed 15
	Damagetype "Enemy"
	-FORCERADIUSDMG
	SeeSound "weapons/nebelwerfer"
	States
	{
	Death:
		FRME A 1 bright A_Explode(40,40)
		FRME BCDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

actor Nebelwerfer : NaziWeapon
{
	Weapon.Kickback 100
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoType "NebAmmo"
	Tag "Klein Nebelwerfer"
	Inventory.PickupMessage "You got the Klein Nebelwerfer!"
	-WEAPON.NOALERT
	-WEAPON.AMMO_CHECKBOTH
	ConversationID 195
	States
	{
	Ready:
		NEBG A 1 A_WeaponReady
		Loop
	Deselect:
		NEBG A 0 A_Lower
		NEBG A 1 A_Lower
		Loop
	Select:
		NEBG A 0 A_Raise
		NEBG A 1 A_Raise
		Loop
	Fire:
		NEBG A 0 A_GunFlash
		NEBG A 0 A_PlaySound("weapons/nebelwerfer",CHAN_WEAPON)
		NEBG A 2 A_FireCustomMissile("NebRocket")
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		NEBG BCDEB 1 A_SetPitch(pitch - 0.5)
		NEBG A 2 A_ReFire
		Goto Ready
	Flash:
		NEBF A 2 bright A_Light2
		Goto LightDone
	Spawn:
		NEBL A -1
		Stop
	}
}

ACTOR NebAmmo : Ammo replaces CellPack
{
	Tag "Mini-rocket"
	Inventory.PickupMessage "Picked up a crate of mini-rockets."
	Inventory.Amount 30
	Inventory.MaxAmount 150
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 300
	ConversationID 177
	Inventory.Icon "MNRBA0"
	States
	{
	Spawn:
		MNRB A -1
		Stop
	}
}

ACTOR NebAmmoSmall : NebAmmo
{
	Inventory.PickupMessage "Picked up some mini-rockets."
	Inventory.Amount 5
	States
	{
	Spawn:
		MNRB A -1	//Needs a sprite
		Stop
	}
}

ACTOR RocketFlameSpawner
{
	Projectile
	Scale 0.15
	Alpha 0.5
	+NOCLIP
	+NOTELEPORT
	+NOBLOCKMAP
	-FLOORCLIP
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 A_CustomMissile("RocketFlame",random(-1,1),random(-1,1),0)
		Stop
	}
}

ACTOR RocketFlame : RocketFlameSpawner
{
	Renderstyle Add
	States
	{
	Spawn:
		XPLO AB 2 BRIGHT
		XPLO C 2 BRIGHT A_CustomMissile("RPG8RocketTrail",1,random(-1,1),0)
		Stop
	}
}

ACTOR RPG8RocketTrail
{
	PROJECTILE
	Height 1
	Radius 1
	Scale 0.18
	Alpha 0.6
	Renderstyle Add
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOP 1
		SMOK Q 1 A_FadeOut
		Stop
	}
}

ACTOR NebelwerferSpawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("Nebelwerfer",0,0,0,velx,vely,velz)
		TNT1 A 0 A_SpawnItemEx("NebAmmoSmall",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}