ACTOR MauserAmmo : Ammo
{
	Tag "7.92x57mm"
	Inventory.Amount 20
	Inventory.MaxAmount 40
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
	Inventory.PickupMessage "Picked up a box of 7.92mm Mauser ammo."
	ConversationID 180
	States
	{
	Spawn:
		M1GA A -1
		Stop
	}
}

ACTOR MauserAmmoSmall : MauserAmmo
{
	Inventory.Amount 5
	Inventory.PickupMessage "Picked up a 7.92mm stripper clip."
	Scale .3
	States
	{
	Spawn:
		M1GA B -1
		Stop
	}
}

ACTOR G43Loaded : Ammo
{
	Tag "7.92x57mm"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon M1GPA0
}

ACTOR G43 : NaziWeapon
{
	Weapon.SelectionOrder 750
	Weapon.Kickback 100
	Weapon.AmmoType1 "G43Loaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "MauserAmmo"
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the Gewehr 43! (slot 5)"
	//Obituary "%o was killed by %k."
	Tag "Gewehr 43"
	+NOAUTOFIRE
	States
	{
	Spawn:
		M1GP A -1
		Stop
	Select:
		M1GG A 0 A_Raise
		M1GG A 1 A_Raise
		Loop
	Deselect:
		M1GG A 0 A_Lower
		M1GG A 1 A_Lower
		Loop
	Ready:
		M1GG A 0 A_JumpIfInventory("G43Loaded",0,2)
		M1GG A 0 A_JumpIfInventory("MauserAmmo",1,2)
		M1GG A 1 A_WeaponReady
		Loop
		M1GG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		M1GG A 0 A_JumpIfInventory("G43Loaded",1,1)
		Goto Reload
		M1GF A 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		M1GF A 0 A_GunFlash
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		M1GF A 0 A_SpawnItemEx("MauserRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M1GF A 1 BRIGHT A_FireCustomMissile("RifleTracer")
		M1GF B 1 BRIGHT A_SetPitch (pitch - 1.4)
		M1GG CDEDC 1 A_AlertMonsters
		TNT1 A 0 A_CheckReload
		M1GG AAFFFA 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		M1GG A 1 Offset(0,35) A_PlaySound("reloads/g43",5)
		M1GG A 1 Offset(-2,38)
		M1GG A 1 Offset(-4,44)
		M1GG A 1 Offset(-6,52)
		M1GG A 1 Offset(-7,57)
		M1GG C 1 Offset(-8,62)
		M1GG C 1 Offset(-9,67)
		M1GG C 1 Offset(-10,72)
		M1GG C 1 Offset(-11,77)
		M1GG C 1 Offset(-11,80)
		M1GG D 1 Offset(-12,82)
		M1GG D 1 Offset(-12,84)
		M1GG D 1 Offset(-13,86)
		M1GG E 1 Offset(-13,87)
		M1GG E 2 Offset(-14,88)
		M1GG E 3 Offset(-14,89)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MauserAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("G43Loaded")
		TNT1 A 0 A_JumpIfInventory("G43Loaded",10,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MauserAmmo",1,"ReloadLoop")
	ReloadFinish:
		M1GG D 2 Offset(-10,96)
		M1GG D 1 Offset(-6,100)
		M1GG C 1 Offset(-7,90)
		M1GG C 1 Offset(-6,80)
		M1GG C 1 Offset(-4,70)
		M1GG A 1 Offset(-2,60)
		M1GG A 1 Offset(0,50)
		M1GG A 1 Offset(0,40)
		M1GG A 1 Offset(0,32)
		Goto Ready
	}
}

ACTOR G43Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("G43Pickup",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("MauserAmmoSmall",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR G43Pickup : CustomInventory
{
	States
	{
	Spawn:
		M1GP A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",12)
		Stop
	}
}