ACTOR ShotgunReloading : Inventory{}

ACTOR ShotgunClip : Ammo
{
	Tag "12 Gauge"
	+IGNORESKILL
	Inventory.MaxAmount 8
}

ACTOR TrenchShotgun : NaziWeapon
{
	Weapon.AmmoType1 "ShotgunClip"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "NewShell"
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the Winchester Trenchgun! (slot 3)"
	+WEAPON.NOAUTOFIRE
	Scale 0.5
	Tag "M1897 Trenchgun"
	ConversationID 194
	States
	{
	Spawn:
		SHOT A -1
		Stop
	Select:
		SHTG A 0 A_Raise
		SHTG A 1 A_Raise
		Loop
	Deselect:
		SHTG A 0 A_TakeInventory("ShotgunReloading")
		SHTG A 0 A_Lower
		SHTG A 1 A_Lower
		Loop
	Ready:
		SHTG A 0 A_JumpIfInventory("ShotgunClip",0,2)
		SHTG A 0 A_JumpIfInventory("NewShell",1,2)
		SHTG A 1 A_WeaponReady
		Loop
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		SHTF A 0 A_JumpIfInventory("ShotgunReloading",1,"ReloadFinish")
		SHTF A 0 A_JumpIfInventory("ShotgunClip",1,1)
		Goto Reload
		SHTF A 0 A_TakeInventory("ShotgunClip",1)
		SHTF A 0 A_PlaySound("Shotgun/Fire",CHAN_WEAPON)
		SHTF AAAAAAAAAA 0 A_FireCustomMissile("ShotgunTracer",frandom(-2.0,2.0),0,0,0,0,frandom(-1.5,1.5))
		SHTF A 1 BRIGHT A_GunFlash
		SHTG A 1 BRIGHT A_SetPitch(pitch - 4.0)
		SHTG A 1 Offset(0,40) A_SetPitch(pitch - 2.0)
		SHTG A 1 Offset(0,36) A_SetPitch(pitch - 1.0)
		SHTG A 1 Offset(0,34) A_SetPitch(pitch + 1.0)
		SHTG A 1 Offset(0,33) A_SetPitch(pitch + 0.5)
		SHTG A 1 A_WeaponReady(WRF_NOFIRE)
		SHTG A 6 A_AlertMonsters
		SHTG A 1 Offset(-1,33)
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		SHTG B 1 Offset(5,34) A_PlaySound("Shotgun/Pump",5)
		SHTG B 1 Offset(2,36)
		SHTG B 1 Offset(-1,38)
		SHTG C 1 Offset(3,35)
		SHTG C 1 Offset(-1,38)
		SHTG D 1 Offset(-4,41)
		SHTG D 1 Offset(-4,43)
		SHTG E 1 Offset(-4,45)
		SHTG E 1 Offset(-3,44)
		SHTG D 1 Offset(-2,40)
		SHTG D 1 Offset(-4,41)
		SHTG C 1 Offset(-1,38)
		SHTG C 1 Offset(-1,37)
		SHTG B 1 Offset(1,36)
		SHTG B 1 Offset(2,36)
		SHTG B 1 Offset(5,34)
		SHTG A 1
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		SHTG A 1 Offset(-1,33) A_GiveInventory("ShotgunReloading",1)
		SHTG B 1 Offset(5,34)
		SHTG B 1 Offset(2,36)
		SHTG B 1 Offset(-1,38)
		SHTG C 1 Offset(3,35)
		SHTG C 1 Offset(-1,38)
		SHTG C 1 Offset(-4,41)
		SHTG C 1 Offset(-6,46)
		SHTG C 1 Offset(-7,55)
	ReloadWork:
		SHTG A 0 A_TakeInventory("NewShell",1,TIF_NOTAKEINFINITE)
		SHTG A 0 A_GiveInventory("ShotgunClip",1)
		SHTG C 1 Offset(-8,58) A_PlaySound("Shotgun/Load",CHAN_WEAPON)
		SHTG C 1 Offset(-9,64)
		SHTG C 1 Offset(-10,70)
		SHTG C 1 Offset(-10,68)
		SHTG C 1 Offset(-9,66)
		SHTG C 1 Offset(-9,64)
		SHTG C 1 Offset(-9,62)
		SHTG C 1 Offset(-8,61) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		SHTG C 1 Offset(-8,60) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		SHTG CC 1 Offset(-8,59) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		SHTG CCC 1 Offset(-8,58) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		SHTG A 0 A_JumpIfInventory("ShotgunClip",8,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("NewShell",1,"ReloadWork")
		Goto ReloadFinish
	ReloadFinish:
		SHTG C 1 Offset(3,35) A_PlaySound("Shotgun/Pump")
		SHTG C 1 Offset(-1,38)
		SHTG D 1 Offset(-4,41)
		SHTG D 1 Offset(-1,33)
		SHTG E 1 Offset(-2,37)
		SHTG E 1 Offset(-3,43)
		SHTG D 1 Offset(-2,40)
		SHTG D 1 Offset(-4,41)
		SHTG C 1 Offset(-1,38)
		SHTG C 1 Offset(3,35)
		SHTG B 1 Offset(-1,38)
		SHTG B 1 Offset(2,36)
		SHTG B 1 Offset(5,34) A_TakeInventory("ShotgunReloading",1)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR TrenchShotgunSpawner replaces Shotgun
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("TrenchShotgun",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("NewShell",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}