ACTOR Walther9mm : NaziWeapon
{
	Weapon.SelectionOrder 9997
	weapon.kickback 60
	Weapon.AmmoType "WaltherAmmo"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "MPClip"
	weapon.ammouse2 1
	Tag "Walther P-38"
	Inventory.PickupMessage "You got the P-38!"
	ConversationID 179
	States
	{
	Ready:
		WALG A 0 A_JumpIfInventory("WaltherAmmo",0,2)
		WALG A 0 A_JumpIfInventory("MPClip",1,2)
		WALG A 1 A_WeaponReady
		Loop
		WALG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		WALG B 0 A_Lower
		WALG B 1 A_Lower
		Loop
	Select:
		WALG A 0 A_Raise
		WALG A 1 A_Raise
		Loop
	Fire:
		WALG A 0 A_JumpIfInventory("WaltherAmmo",1,1)
		Goto Reload
		WALF A 0 A_GunFlash
		WALF A 0 A_SetPitch(pitch - 0.2)
		WALF A 0 A_PlaySound("weapons/Walther",CHAN_WEAPON)
		WALF A 1 BRIGHT A_FireCustomMissile("PistolTracer",frandom(-1,1))
		WALG B 2 A_AlertMonsters
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		WALG A 0 A_SetPitch(pitch - 0.2)
		WALG A 1
		WALG A 1 Offset(0,36)
		WALG A 1 Offset(0,41)
		WALG A 1 Offset(0,35)
		TNT1 A 0 A_CheckReload
		WALG A 1 Offset(0,32) A_Jump(128,"Ready")
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		WALG A 1 Offset(0,35) A_PlaySound("reloads/p38",5)
		WALG A 1 Offset(0,38)
		WALG A 1 Offset(0,44)
		WALG A 1 Offset(0,52)
		WALG B 1 Offset(0,62)
		WALG B 1 Offset(0,72)
		WALG B 1 Offset(0,82)
		TNT1 A 8
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("WaltherAmmo",1)
		TNT1 A 0 A_JumpIfInventory("WaltherAmmo",8,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MPClip",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		WALG B 1 Offset(0,82)
		WALG B 1 Offset(0,72)
		WALG B 1 Offset(0,62)
		WALG B 1 Offset(0,52)
		WALG A 1 Offset(0,44)
		WALG A 1 Offset(0,38)
		WALG A 1 Offset(0,35)
		WALG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	Spawn:
		WALP A -1
		Stop
	}
}

ACTOR WaltherAmmo : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon "LUGPA0"
}