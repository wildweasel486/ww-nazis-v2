ACTOR BigNazi
{
	scale 1.1
	health 300
	painchance 110
	height 56
	mass 200
	speed 8
	DamageFactor "Enemy",0.0
	MONSTER
	+FLOORCLIP
	+NOTARGET
	+NOINFIGHTING
	+LOOKALLAROUND
	painsound "boss/pain"
	States
	{
	Idle:
		"####" AA 4 A_Wander
		"####" A 0 A_Look
		"####" BB 4 A_Wander
		"####" A 0 A_Look
		"####" CC 4 A_Wander
		"####" A 0 A_Look
		"####" DD 4 A_Wander
		"####" A 0 A_Look
		loop
	}
}
ACTOR Nazi : BigNazi
{
	-LOOKALLAROUND
	health 20
	mass 100
	painchance 200
	seesound "Nazi1/Sighted"
	painsound "Nazi1/Pain"
	deathsound "Nazi1/Death"
	States
	{
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" L 5
		"####" M -1
		stop
	Death.Fire:
		TNT1 A 0 A_SetScale(1.0)
		BURN ABC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("death/burning")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	}
}

ACTOR GuardSpawner : RandomSpawner replaces Zombieman
{
	dropitem Guard
	dropitem SSGuard
	dropitem WGuard
}

ACTOR Guard : Nazi
{
	obituary "%o was shot by a pistol soldier."
	DropItem "Walther9mm"
	DropItem "MPMagazine"
	states
	{
	Spawn:
		GARD N 10 A_Look
		loop
	See:
		GARD AABBCCDD 4 A_Chase
		loop
	Missile:
		GARD EF 5 A_FaceTarget
		GARD G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GARD G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		GARD F 8
		goto See
	Pain:
		GARD H 3
		GARD H 3 A_Pain
		goto See
	Raise:
		GARD MLKJI 5
		goto See
	}
}

ACTOR SSGuard : Guard
{
	obituary "%o was killed by an SS guard."
	health 40
	states
	{
	Spawn:
		SSPG N 10 A_Look
		loop
	See:
		SSPG AABBCCDD 3 A_Chase
		loop
	Missile:
		SSPG EF 5 A_FaceTarget
		SSPG G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		SSPG G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		SSPG F 8
		goto See
	Pain:
		SSPG H 3
		SSPG H 3 A_Pain
		goto See
	Raise:
		SSPG MLKJI 5
		goto See
	}
}

ACTOR WGuard : Guard
{
	obituary "%o was shot by a Wehrmacht soldier."
	health 30
	speed 10
	painchance 185
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
	Spawn:
		GRD2 N 10 A_Look
		loop
	See:
		GRD2 AABBCCDD 4 A_Chase
		loop
	Missile:
		GRD2 EF 2 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		GRD2 F 4
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-10,10))
		GRD2 F 8
		goto See
	Pain:
		GRD2 H 3
		GRD2 H 3 A_Pain
		goto See
	Raise:
		GRD2 MLKJI 5
		goto See
	}
}

ACTOR MP40GuardSpawner : RandomSpawner replaces ShotgunGuy
{
	dropitem MP40Guard
	dropitem WMP40Guard
	dropitem SSMP40Guard
}

ACTOR MP40GuardSpawner2 : MP40GuardSpawner replaces WolfensteinSS{}

ACTOR MP40Guard : Nazi
{
	obituary "%o was plugged by a soldier with an MP40."
	seesound "Nazi2/Sighted"
	painsound "Nazi2/Pain"
	deathsound "Nazi2/Death"
	dropitem "MP40"
	DropItem "MPMagazine"
	DropItem "MPMagazine"
	dropitem "GrenadePickup" 16
	states
	{
	Spawn:
		MGRD N 10 A_Look
		loop
	See:
		MGRD AABBCCDD 4 A_Chase
		loop
	Missile:
		MGRD EF 5 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2
		goto See
	Pain:
		MGRD H 3
		MGRD H 3 A_Pain
		goto See
	Raise:
		MGRD MLKJI 5
		goto See
	}
}

ACTOR WMP40Guard : MP40Guard
{
	obituary "%o was plugged by a trigger-happy Wehrmacht."
	health 30
	speed 10
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
	Spawn:
		MGR2 N 10 A_Look
		loop
	See:
		MGR2 AABBCCDD 4 A_Chase
		loop
	Missile:
		MGR2 EF 2 A_FaceTarget
		MGR2 G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGR2 F 1
		MGR2 G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-12,12))
		MGR2 F 2 A_SentinelRefire
		goto Missile+2
	Pain:
		MGR2 H 3
		MGR2 H 3 A_Pain
		goto See
	Raise:
		MGR2 MLKJI 5
		goto See
	}
}

ACTOR SSMP40Guard : MP40Guard
{
	obituary "%o was plugged by an SS with an MP40."
	health 40
	states
	{
	Spawn:
		SSMG N 10 A_Look
		loop
	See:
		SSMG AABBCCDD 3 A_Chase
		loop
	Missile:
		SSMG EF 5 A_FaceTarget
		SSMG G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		SSMG G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		SSMG F 2 A_SentinelRefire
		goto Missile+2
	Pain:
		SSMG H 3
		SSMG H 3 A_Pain
		goto See
	Raise:
		SSMG MLKJI 5
		goto See
	}
}

ACTOR RiflemanSpawner : RandomSpawner replaces ChaingunGuy
{
	dropitem RifleGuard
	dropitem WRifleGuard
	dropitem WaffenSS
}

ACTOR RifleGuard : Nazi
{
	obituary "%o was plinked by a rifle soldier."
	seesound "Nazi3/Sighted"
	painsound "Nazi2/Pain"
	deathsound "Nazi2/Death"
	DropItem G43Spawner
	states
	{
	Spawn:
		RGRD N 10 A_Look
		loop
	See:
		RGRD AABBCCDD 4 A_Chase
		loop
	Missile:
		RGRD EF 10 A_FaceTarget
		RGRD G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGRD G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGRD F 8
		goto See
	Pain:
		RGRD H 3
		RGRD H 3 A_Pain
		goto See
	Raise:
		RGRD MLKJI 5
		goto See
	}
}

ACTOR WRifleGuard : RifleGuard
{
	obituary "%o was snap-shot by a rifle soldier."
	health 30
	speed 10
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
	Spawn:
		RGR2 N 10 A_Look
		loop
	See:
		RGR2 AABBCCDD 4 A_Chase
		loop
	Missile:
		RGR2 EF 10 A_FaceTarget
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGR2 F 4
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-5,5))
		RGR2 F 8
		goto See
	Pain:
		RGR2 H 3
		RGR2 H 3 A_Pain
		goto See
	Raise:
		RGR2 MLKJI 5
		goto See
	}
}

ACTOR WaffenSS : RifleGuard
{
	obituary "%o was shot to pieces by a Waffen-SS."
	health 80
	dropitem "STG44"
	DropItem STGClip
	dropitem "GrenadePickup" 32
	states
	{
	Spawn:
		WAFF N 10 A_Look
		loop
	See:
		WAFF AABBCCDD 3 A_Chase
		loop
	Missile:
		WAFF EF 5 A_FaceTarget
		WAFF G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		WAFF G 2 BRIGHT A_CustomMissile("EnemyStGTracer",32,0,random(-4,4))
		WAFF F 4 A_SentinelRefire
		goto Missile+2
	Pain:
		WAFF H 3
		WAFF H 3 A_Pain
		goto See
	Raise:
		WAFF MLKJI 5
		goto See
	}
}

ACTOR SpectreSpawner : RandomSpawner replaces Spectre
{
	DropItem "Paratrooper"
	DropItem "Mutant"
}

ACTOR Paratrooper : RifleGuard
{
	obituary "%o was slaughtered by a Fallschirmjaeger."
	health 90
	dropitem "FG42Spawner" 128
	dropitem "MauserAmmoSmall" 128
	states
	{
	Spawn:
		PARA N 10 A_Look
		loop
	See:
		PARA AABBCCDD 3 A_Chase
		loop
	Missile:
		PARA EF 5 A_FaceTarget
		PARA G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		PARA G 2 BRIGHT A_CustomMissile("EnemyStGTracer",32,0,random(-4,4))
		PARA F 4 A_SentinelRefire
		goto Missile+2
	Pain:
		PARA H 3
		PARA H 3 A_Pain
		goto See
	Raise:
		PARA MLKJI 5
		goto See
	}
}

ACTOR Mutant : Nazi
{
	obituary "%o did the dance of doom with a Mutant."
	health 90
	speed 6
	seesound ""
	painsound ""
	deathsound "mutant/Death"
	BloodColor "Green"
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
	Spawn:
		MTNT A 10 A_Look
		loop
	See:
		MTNT BBCCDDEE 2 A_Chase
		loop
	Missile:
		MTNT F 6 A_FaceTarget
		MTNT G 0 A_PlaySound("weapons/Walther",CHAN_WEAPON)
		MTNT G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6
		MTNT H 0 A_PlaySound("weapons/Walther",CHAN_WEAPON)
		MTNT H 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6 A_SentinelRefire
		goto Missile+2
	Pain:
		MTNT I 3
		MTNT I 3 A_Pain
		goto See
	Death:
	Death.Fire:
		MTNT I 3
		MTNT J 3 A_Scream
		MTNT K 3 A_NoBlocking
		MTNT LM 3
		MTNT N -1
		stop
	Raise:
		MTNT MLKJI 5
		goto See
	}
}

ACTOR OfficerSpawner : RandomSpawner replaces DoomImp
{
	dropitem Officer
	dropitem WOfficer
	dropitem SSOfficer
}

ACTOR Officer : Nazi
{
	obituary "%o was executed by an officer."
	seesound "Nazi4/Sighted"
	painsound "Nazi4/Pain"
	deathsound "Nazi4/Death"
	dropitem "Luger9mm"
	DropItem "MPMagazine"
	states
	{
	Spawn:
		OFFI N 10 A_Look
		loop
	See:
		OFFI AABBCCDD 2 A_Chase
		loop
	Missile:
		OFFI EF 2 A_FaceTarget
		OFFI G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFFI G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		OFFI F 4
		goto See
	Pain:
		OFFI H 3
		OFFI H 3 A_Pain
		goto See
	Raise:
		OFFI MLKJI 5
		goto See
	}
}

ACTOR WOfficer : Officer
{
	obituary "%o was quickly executed by an Officer."
	health 30
	speed 10
	painchance 180
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
	Spawn:
		OFR2 N 10 A_Look
		loop
	See:
		OFR2 AABBCCDD 2 A_Chase
		loop
	Missile:
		OFR2 EF 2 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		OFR2 F 3
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		OFR2 F 3
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 3
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 4
		goto See
	Pain:
		OFR2 H 3
		OFR2 H 3 A_Pain
		goto See
	Raise:
		OFR2 MLKJI 5
		goto See
	}
}

ACTOR SSOfficer : Officer
{
	obituary "%o was viciously executed by an SS officer."
	health 40
	states
	{
	Spawn:
		SSOF N 10 A_Look
		loop
	See:
		SSOF AABBCCDD 2 A_Chase
		loop
	Missile:
		SSOF EF 2 A_FaceTarget
		SSOF G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		SSOF G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		SSOF F 4
		goto See
	Pain:
		SSOF H 3
		SSOF H 3 A_Pain
		goto See
	Raise:
		SSOF MLKJI 5
		goto See
	}
}

ACTOR DogSpawner : RandomSpawner replaces Demon
{
	dropitem Doberman
	dropitem GermanShepherd
}

ACTOR Doberman
{
	hitobituary "%o was bitten by a guard dog."
	health 25
	painchance 180
	speed 16
	height 48
	mass 70
	seesound "dog/sight"
	attacksound "dog/attack"
	painsound "dog/pain"
	deathsound "dog/death"
	activesound "dog/active"
	MONSTER
	+FLOORCLIP
	-CANUSEWALLS
	states
	{
	Spawn:
		DOGY AA 2 A_Wander
		DOGY A 0 A_Look
		DOGY BB 2 A_Wander
		DOGY B 0 A_Look
		DOGY CC 2 A_Wander
		DOGY C 0 A_Look
		DOGY DD 2 A_Wander
		DOGY D 0 A_Look
		Loop
	See:
		"####" AABBCCDD 2 A_Chase
		loop
	Melee:
		"####" EF 2 Fast A_FaceTarget
		"####" G 8 Fast A_CustomMeleeAttack(random(1,8)*5)
		"####" FE 4 Fast
		goto See
	Pain:
		"####" H 2
		"####" H 2 A_Pain
		goto See
	Death:
		"####" H 8
		"####" I 8 A_Scream
		"####" J 6
		"####" K -1 A_NoBlocking
		stop
	Raise:
		"####" KJIH 6
		Goto See
	}
}

ACTOR GermanShepherd : Doberman
{
	states
	{
	Spawn:
		DOG2 AA 2 A_Wander
		DOG2 A 0 A_Look
		DOG2 BB 2 A_Wander
		DOG2 B 0 A_Look
		DOG2 CC 2 A_Wander
		DOG2 C 0 A_Look
		DOG2 DD 2 A_Wander
		DOG2 D 0 A_Look
		Loop
	}
}

ACTOR RevenantSpawner : RandomSpawner replaces Revenant
{
	DropItem "RocketMan"
	DropItem "RocketMan"
	DropItem "MadDoctor"
}

ACTOR RocketMan : BigNazi
{
	obituary "%o was splattered by Otto Giftmacher."
	seesound "Boss2/Sighted"
	deathsound "Boss2/Death"
	states
	{
	Spawn:
		OTTO A 10 A_Look
		loop
	See:
		"####" AABBCCDD 4 A_Chase
		loop
	Missile:
		OTTO E 10 A_FaceTarget
		OTTO F 8 BRIGHT A_CustomMissile("MiniRocket",32,12,0)
		OTTO E 8
		goto See
	Pain:
		OTTO A 3
		OTTO A 3 A_Pain
		goto See
	Death:
		"####" G 5
		"####" H 5 A_Scream
		"####" I 5
		"####" J -1 A_NoBlocking
		stop
	Raise:
		"####" JIHG 5
		goto See
	}
}

ACTOR MadDoctor : RocketMan
{
	obituary "%o was used as an unwitting test subject by Dr. Schabbs."
	states
	{
	Spawn:
		SCHB A 10 A_Look
		loop
	Missile:
		SCHB E 10 A_FaceTarget
		SCHB F 8 A_CustomMissile("FlyingNeedle",32,12,0)
		SCHB A 8
		goto See
	Pain:
		SCHB G 3
		SCHB A 3 A_Pain
		goto See
	}
}

ACTOR FlyingNeedle
{
	Radius 11
	Height 8
	Speed 20
	Damage 8
	Gravity 0.2
	Projectile
	+RANDOMIZE
	+STRIFEDAMAGE
	DamageType "Enemy"
	States
	{
	Spawn:
		NEDL ABCD 3
		loop
	Death:
		TNT1 A 1
		stop
	}
}

ACTOR SuperSoldierSpawner : RandomSpawner replaces HellKnight
{
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldier"
	DropItem "SuperSoldierGirl"
}

ACTOR SuperSoldier : BigNazi
{
	obituary "%o was terminated by an ubersoldat."
	health 500
	radius 24
	height 64
	mass 500
	painchance 150
	seesound "Boss1/Sighted"
	deathsound "Boss1/Death"
	DropItem "UMG43Spawner" 8
	+NOBLOOD
	states
	{
	Spawn:
		HANS A 10 A_Look
		loop
	See:
		HANS AABBCCDD 4 A_Chase
		loop
	Missile:
		HANS E 20 A_FaceTarget
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 BRIGHT A_SentinelRefire
		goto Missile+1
	Pain:
		HANS H 3
		HANS H 3 A_Pain
		goto See
	Death:
		HANS I 5
		HANS J 5 A_Scream
		HANS K 5
		HANS L -1 A_NoBlocking
		stop
	Raise:
		HANS LKJI 5
		goto See
	}
}

ACTOR SuperSoldierGirl : SuperSoldier
{
	obituary "%o was terminated by a female ubersoldat."
	seesound "BossGirl/Sighted"
	painsound "bossGirl/pain"
	deathsound "BossGirl/Death"
	DropItem ""
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOBLOOD
	states
	{
	Spawn:
		WBO4 A 10 A_Look
		loop
	See:
		WBO4 AABBCCDD 4 A_Chase
		loop
	Missile:
		WBO4 E 20 A_FaceTarget
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 F 2 BRIGHT A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 E 2 BRIGHT
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 F 2 BRIGHT A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 E 2 BRIGHT
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 F 2 BRIGHT A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 E 2 BRIGHT
		Goto See
	Pain:
		WBO4 H 3
		WBO4 H 3 A_Pain
		goto See
	Death:
		WBO4 H 5
		WBO4 I 5
		WBO4 J 5 A_Scream
		WBO4 K -1 A_NoBlocking
		stop
	Raise:
		WBO4 KJI 5
		goto See
	}
}

ACTOR SuperSoldierElite : SuperSoldier replaces BaronOfHell
{
	obituary "%o was pulverized by an elite ubersoldat."
	health 850
	+BOSSDEATH
	states
	{
	Spawn:
		TRAN A 10 A_Look
		loop
	See:
		TRAN AABBCCDD 4 A_Chase
		loop
	Missile:
		TRAN E 20 A_FaceTarget
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 BRIGHT A_SentinelRefire
		goto Missile+1
	Pain:
		TRAN H 3
		TRAN H 3 A_Pain
		goto See
	Death:
		TRAN I 5
		TRAN J 5 A_Scream
		TRAN K 5 A_BossDeath
		TRAN L -1 A_NoBlocking
		stop
	Raise:
		TRAN LKJI 5
		goto See
	}
}

ACTOR MiniRocket
{
	Radius 11
	Height 8
	Speed 15
	Damage 5
	Projectile
	+RANDOMIZE
	DamageType "Enemy"
	SeeSound "weapons/rocklf"
	DeathSound "weapons/rocklx"
	States
	{
	Spawn:
		MISL A 1 bright A_CustomMissile("RocketFlameSpawner",0,0,0,5)
		loop
	Death:
		FRME A 1 bright A_Explode(20,40)
		FRME BCDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR MiniMiniRocket : MiniRocket
{
	Scale 0.8
	Damage 4
	+STRIFEDAMAGE
	DeathSound "weapons/nebxpl"
}

ACTOR General : BigNazi replaces Archvile
{
	obituary "%o was eliminated by a Nazi general."
	health 800
	mass 1000
	seesound "Boss3/Sighted"
	deathsound "Boss3/Death"
	DropItem "NebelwerferSpawner" 64
	DropItem "UMG43Spawner" 8
	+MISSILEEVENMORE
	states
	{
	Spawn:
		FETT A 10 A_Look
		loop
	See:
		FETT AABBCCDD 4 A_Chase
		loop
	Missile:
		FETT E 10 A_FaceTarget
		FETT E 0 A_Jump(128,"Chaingun")
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15,0)
		FETT E 8
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15,0)
		FETT E 8
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15,0)
		FETT E 8
		goto See
	Chaingun:
		FETT G 0 A_FaceTarget
		FETT H 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		FETT H 2 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,12,random(-11,11))
		FETT G 2 BRIGHT A_SentinelRefire
		goto Chaingun+1
	Pain:
		FETT D 3
		FETT D 3 A_Pain
		goto See
	Death:
		FETT I 5
		FETT J 5 A_Scream
		FETT K 5
		FETT L -1 A_NoBlocking
		stop
	Raise:
		FETT LKJI 5
		goto See
	}
}

ACTOR FlamerMan : BigNazi replaces Arachnotron
{
	obituary "%o was roasted by a Nazi with a flamethrower."
	health 500
	seesound "Boss1/Sighted"
	deathsound "Boss1/Death"
	dropitem "Pyrolight"
	DropItem FlameAmmo
	states
	{
	Spawn:
		FLAM A 10 A_Look
		loop
	See:
		FLAM AABBCCDD 3 A_Chase
		loop
	Missile:
		FLAM E 10 A_FaceTarget
		FLAM E 0 A_JumpIfCloser(384,"ProperMissile")
		FLAM F 4 BRIGHT A_CustomMissile("EnemyFlamebolt",32,12,0)
		FLAM E 6
		Goto See
	ProperMissile:
		FLAM FFFFFFFF 1 BRIGHT A_CustomMissile("EnemyFlamerShot",32,12,0)
		FLAM E 6 A_SpidRefire
		goto Missile
	Pain:
		FLAM A 3
		FLAM A 3 A_Pain
		goto See
	Death:
		FLAM G 5
		FLAM H 5 A_Scream
		FLAM I 5 A_BossDeath
		FLAM J -1 A_NoBlocking
		stop
	Raise:
		FLAM JIHG 5
		goto See
	}
}

ACTOR ChairGuy : BigNazi replaces PainElemental
{
	obituary "%o was given the chair."
	health 600
	mass 2000
	seesound "krist/sight"
	deathsound "krist/death"
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	states
	{
	Spawn:
		KRIS A 10 A_Look
		loop
	See:
		KRIS AABBCCDD 4 A_Chase
		loop
	Missile:
		KRIS E 10 A_FaceTarget
		KRIS F 8 BRIGHT A_CustomMissile("MiniRocket",48,15,0)
		KRIS E 8
		KRIS G 8 BRIGHT A_CustomMissile("MiniRocket",48,-15,0)
		KRIS E 8
		goto See
	Pain:
		KRIS A 3
		KRIS A 3 A_Pain
		goto See
	Death:
		KRIS H 5
		KRIS I 5 A_Scream
		KRIS J 5
		KRIS K -1
		Wait
	Crash:
		KRIS H 0 A_SetFloorClip
		KRIS H 0 A_PlaySound("weapons/rocklx")
		MISL B 8 bright A_Explode(40,40)
		MISL C 6 bright
		MISL D 4 bright
		Stop
	}
}

ACTOR HitlerGhost replaces Cacodemon
{
	Scale 1.2
	obituary "%o thought %g saw Hitler."
	health 250
	height 56
	mass 1000
	speed 13
	painchance 24
	deathsound "hitlerghost/die"
	seesound "hitlerghost/see"
	DamageFactor "enemy", 0.0
	MONSTER
	+FLOAT
	+NOGRAVITY
	+NOTARGET
	+NOINFIGHTING
	+LOOKALLAROUND
	+NOBLOOD
	States
	{
	Spawn:
		HGST A 10 A_Look
		Loop
	See:
		HGST A 6 A_FastChase
		HGST BCB 6 A_Chase
		Loop
	Missile:
		HGST A 4 A_FaceTarget
		HGST D 4 BRIGHT A_CustomMissile("EnemyFlamebolt2",30)
		HGST A 4
		Goto See
	Pain:
		HGST A 4 A_Pain
		Goto See
	Death:
		HGST E 6 A_Scream
		HGST FGHI 6
		HGST J -1 A_NoBlocking
		Stop
	}
}

ACTOR MiniShip
{
	scale 1.1
	obituary "%o received death from above."
	health 375
	height 56
	mass 2000
	speed 13
	painchance 200
	attacksound "weapons/stg44"
	painsound "metal/pain"
	deathsound "weapons/rocklx"
	DamageFactor "Enemy", 0.0
	MONSTER
	+FLOAT
	+NOGRAVITY
	+NOTARGET
	+NOINFIGHTING
	+LOOKALLAROUND
	+NOBLOOD
	states
	{
	Spawn:
		SHIP A 10 A_Look
		loop
	See:
		SHIP AABBCCDD 4 A_Chase
		loop
	Missile:
		SHIP D 10 A_FaceTarget
		SHIP E 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		SHIP E 4 BRIGHT A_CustomMissile("EnemyChaingunTracer",20,4,random(-11,11))
		SHIP D 4
		SHIP E 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		SHIP F 4 BRIGHT A_CustomMissile("EnemyChaingunTracer",20,-4,random(-11,11))
		SHIP D 4
		goto See
	Pain:
		SHIP A 3
		SHIP A 3 A_Pain
		goto See
	Death:
		SHIP B 0 A_Scream
		MISL B 8 bright A_Explode(40,40)
		MISL C 6 bright
		MISL D 4 bright
		Stop
	Idle:
		SHIP AA 4 A_Wander
		SHIP A 0 A_Look
		SHIP BB 4 A_Wander
		SHIP A 0 A_Look
		SHIP CC 4 A_Wander
		SHIP A 0 A_Look
		SHIP DD 4 A_Wander
		SHIP A 0 A_Look
		loop
	}
}

ACTOR Geist replaces LostSoul
{
	Scale 0.8
	obituary "%o had %p soul removed by a Geist."
	health 100
	radius 16
	height 56
	mass 1000
	speed 15
	painchance 255
	renderstyle translucent
	alpha 0.5
	deathsound "geist/die"
	painsound "geist/pain"
	activesound "brain/cube"
	DamageFactor "enemy", 0.0
	MONSTER
	+FLOAT
	+NOGRAVITY
	+NOTARGET
	+NOINFIGHTING
	+LOOKALLAROUND
	+DONTFALL
	+NOBLOOD
	States
	{
	Spawn:
		GEST A 10 A_Look
		Loop
	See:
		GEST A 0 A_LoopActiveSound
		GEST A 0 A_SetTranslucent(0.1,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.2,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.3,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.4,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.5,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.4,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.3,0)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.2,0)
		GEST ABCD 3 A_Chase
		Loop
	Melee:
		GEST A 1 A_SetTranslucent(0.1)
		GEST A 1 A_SetTranslucent(0.3)
		GEST A 1 A_SetTranslucent(0.5)
		GEST B 3 Bright A_FaceTarget
		GEST CDABCD 3 BRIGHT A_CustomMeleeAttack(4)
		GEST A 1 A_SetTranslucent(0.4)
		GEST A 1 A_SetTranslucent(0.3)
		GEST A 1 A_SetTranslucent(0.2)
		GEST A 1 A_SetTranslucent(0.1)
		Goto See
	Pain:
		GEST E 1 A_Pain
		GEST E 1 A_SetTranslucent(0.4)
		GEST E 1 A_SetTranslucent(0.3)
		GEST E 1 A_SetTranslucent(0.2)
		GEST E 1 A_SetTranslucent(0.1)
		Goto See
	Death:
		GEST E 5 A_SetTranslucent(0.5)
		GEST E 6 A_Scream
		GEST FG 6
		GEST H 6 A_NoBlocking
		Stop
	}
}

ACTOR MGTurret // replaces LostSoul
{
	obituary "%o was shot up by a gun turret."
	health 100
	radius 24
	height 24
	mass 10000000000000000
	painchance 100
	painsound "metal/pain"
	deathsound "weapons/rocklx"
	MONSTER
	+MISSILEEVENMORE
	+NOGRAVITY
	+LOOKALLAROUND
	+NOBLOOD
	states
	{
	Spawn:
		GTUR A 1 A_Look
		loop
	See:
		GTUR A 4 A_Chase
		loop
	Pain:
		GTUR A 3
		GTUR A 3 A_Pain
		goto See
	Missile:
		GTUR A 10 A_FaceTarget
		GTUR B 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		GTUR C 3 bright A_CustomMissile("EnemyChaingunTracer",20,0,random(-11,11))
		GTUR B 3 bright A_CPosRefire
		goto Missile+1
	Death:
		TNT1 A 0 A_Scream
		GTUR D 6 bright A_Explode(40,40)
		GTUR EFG 6 bright
		GTUR H -1 A_NoBlocking
		Stop
	}
}

ACTOR DeathKnight : BigNazi replaces Cyberdemon
{
	scale 1.5
	obituary "%o knelt before the Death Knight."
	health 3000
	radius 64
	height 96
	mass 1000
	speed 14
	painchance 20
	seesound "deathknight/sight"
	deathsound "deathknight/death"
	dropitem "NebelwerferSpawner"
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	states
	{
	Spawn:
		DKGT A 10 A_Look
		loop
	See:
		DKGT AABB 4 A_Chase
		DKGT A 0 A_Hoof
		DKGT CCDD 4 A_Chase
		DKGT A 0 A_Hoof
		Loop
	Missile:
		DKGT E 10 A_FaceTarget
		DKGT E 0 A_Jump(40, "AlphaStrike")
		DKGT E 0 A_Jump(128,"Chaingun")
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24,0)
		DKGT E 3 A_CPosRefire
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24,0)
		DKGT E 3 A_CPosRefire
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24,0)
		DKGT E 3 A_CPosRefire
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24,0)
		DKGT E 3
		goto See
	Chaingun:
		DKGT H 0 A_FaceTarget
		DKGT K 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT II 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT H 2 bright A_SentinelRefire
		goto Chaingun+1
	AlphaStrike:
		DKGT K 0 A_FaceTarget
		DKGT K 0 A_CustomMissile("NebRocketEnemy",80,24,0)
		DKGT K 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT KK 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT L 0 A_CustomMissile("NebRocketEnemy",80,-24,0)
		DKGT L 2 bright A_SentinelRefire
		goto AlphaStrike+1
	Pain:
		DKGT E 3
		DKGT E 3 A_Pain
		goto See
	Death:
		DKGT M 5 A_Scream
		DKGT NOP 5
		DKGT Q 5 A_NoBlocking
		DKGT R 5
		DKGT S -1 A_BossDeath
		stop
	Idle:
		Stop
	}
}

ACTOR Hitler : BigNazi replaces SpiderMastermind
{
	obituary "%o heiled Hitler."
	health 2500
	radius 32
	height 64
	mass 1000
	speed 10
	painchance 20
	seesound "hitler/sight"
	painsound "hitler/pain"
	deathsound "hitler/death"
	activesound "hitler/active"
	DropItem "UMG43Spawner" 64
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	states
	{
	Spawn:
		HITL A 10 A_Look
		loop
	See:
		HITL AABB 4 A_Chase
		HITL A 0 A_Hoof
		HITL CCDD 4 A_Chase
		HITL A 0 A_Hoof
		Loop
	Missile:
		HITL E 10 A_FaceTarget
		HITL F 0 A_FaceTarget
		HITL F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",32,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-24,random(-3,3))
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",48,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",48,-24,random(-3,3))
		HITL G 2 BRIGHT A_SentinelRefire
		goto Missile+1
	Pain:
		HITL Q 3
		HITL Q 3 A_Pain
		goto See
	Death:
		HITL H 10 A_Scream
		HITL IJK 10
		HITL L 10 A_PlaySound("hitler/gib")
		HITL M 10 A_NoBlocking
		HITL NO 10
		HITL P -1 A_BossDeath
		stop
	Idle:
		Stop
	}
}

ACTOR FatMan : BigNazi replaces Fatso
{
	obituary "%o was vaporized by a Nazi's double rocket penetration."
	health 600
	mass 1000
	seesound "fatman/sight"
	deathsound "fatman/death"
	DropItem "NebelwerferSpawner" 64
	DropItem "GrenadePickup" 128
	+MISSILEEVENMORE
	states
	{
	Spawn:
		GOER A 10 A_Look
		loop
	See:
		GOER AABBCCDD 4 A_Chase
		loop
	Missile:
		GOER E 10 A_FaceTarget
		GOER F 6 BRIGHT A_CustomMissile("MiniMiniRocket",32,15,0)
		GOER E 6
		GOER G 6 BRIGHT A_CustomMissile("MiniMiniRocket",32,-15,0)
		GOER E 6
		GOER F 6 BRIGHT A_CustomMissile("MiniMiniRocket",32,15,0)
		GOER E 6
		GOER G 6 BRIGHT A_CustomMissile("MiniMiniRocket",32,-15,0)
		goto See
	Pain:
		GOER D 3
		GOER D 3 A_Pain
		goto See
	Death:
		GOER H 5
		GOER I 5 A_Scream
		GOER J 5
		GOER K 0 A_NoBlocking
		GOER K -1 A_BossDeath
		stop
	Raise:
		GOER KJIH 5
		goto See
	}
}

ACTOR NaziPuff : BulletPuff
{
	DamageType "Enemy"
}