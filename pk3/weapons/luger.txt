ACTOR Luger9mm : NaziWeapon
{
	Weapon.SelectionOrder 9999
	weapon.kickback 60
	Weapon.AmmoType "LugerAmmo"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "MPClip"
	weapon.ammouse2 1
	weapon.ammogive2 10
	Tag "Luger P-08"
	Inventory.PickupMessage "You got the Luger!"
	+Weapon.NoAutoFire
	States
	{
	Ready:
		LUGG A 0 A_JumpIfInventory("LugerAmmo",0,2)
		LUGG A 0 A_JumpIfInventory("MPClip",1,2)
		LUGG A 1 A_WeaponReady
		Loop
		LUGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		LUGG A 0 A_Lower
		LUGG A 1 A_Lower
		Loop
	Select:
		LUGG A 0 A_Raise
		LUGG A 1 A_Raise
		Loop
	Fire:
		LUGG A 0 A_JumpIfInventory("LugerAmmo",1,1)
		Goto Reload
		LUGF A 0 BRIGHT A_GunFlash
		LUGF A 0 A_SetPitch(pitch - 0.2)
		LUGF A 0 A_PlaySound("weapons/Luger",CHAN_WEAPON)
		LUGF A 0 A_TakeInventory("LugerAmmo",1)
		LUGF A 1 BRIGHT A_FireCustomMissile("LugerTracer",frandom(-1,1),0)
		LUGG B 1 A_AlertMonsters
		LUGG A 0 A_SetPitch(pitch - 0.2)
		LUGG B 1 Offset(0,36)
		LUGG B 1 Offset(0,41)
		LUGG C 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		LUGG C 1 Offset(0,33) A_WeaponReady(WRF_NOBOB)
		LUGG C 1 Offset(0,31) A_WeaponReady(WRF_NOBOB)
		LUGG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		LUGG A 1 Offset(0,35) A_PlaySound("reloads/luger", CHAN_ITEM)
		LUGG A 1 Offset(0,38)
		LUGG A 1 Offset(0,44)
		LUGG A 1 Offset(0,52)
		LUGG C 1 Offset(0,62)
		LUGG C 1 Offset(0,72)
		LUGG C 1 Offset(0,82)
		TNT1 A 11
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("LugerAmmo",1)
		TNT1 A 0 A_JumpIfInventory("LugerAmmo",8,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MPClip",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		LUGG B 1 Offset(0,82)
		LUGG B 1 Offset(0,72)
		LUGG B 1 Offset(0,62)
		LUGG C 1 Offset(0,52)
		LUGG C 1 Offset(0,44)
		LUGG C 1 Offset(0,38)
		LUGG A 1 Offset(0,35)
		LUGG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	Spawn:
		LUGP A -1
		Stop
	}
}

ACTOR LugerAmmo : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon "LUGPA0"
}