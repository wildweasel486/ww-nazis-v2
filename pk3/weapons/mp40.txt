ACTOR MP40Seq : Inventory{}

ACTOR MP40 : NaziWeapon
{
	scale 0.5
	Weapon.SelectionOrder 600
	weapon.kickback 60
	Weapon.AmmoType "MPAmmo"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "MPClip"
	weapon.ammouse2 1
	weapon.ammogive2 20
	Tag "MP-40"
	Inventory.PickupMessage "You got the MP-40!"
	ConversationID 188
	States
	{
	Ready:
		RIFG A 0 A_JumpIfInventory("MPAmmo",0,2)
		RIFG A 0 A_JumpIfInventory("MPClip",1,2)
		RIFG A 1 A_WeaponReady
		Loop
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		RIFG B 0 A_Lower
		RIFG B 1 A_Lower
		Loop
	Select:
		RIFG A 0 A_Raise
		RIFG A 1 A_Raise
		Loop
	Fire:
		RIFF A 0 A_JumpIfInventory("MPAmmo",1,1)
		Goto Reload
		RIFF A 0 Offset(0,32) A_GunFlash
		RIFF A 0 A_SetPitch(pitch - 0.5)
		RIFF A 0 A_PlaySound("weapons/mp40",CHAN_WEAPON)
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		RIFF A 1 BRIGHT A_FireCustomMissile("SMGTracer",frandom(-1.4,1.4))
		RIFF B 1 Offset(0,38) A_AlertMonsters
		RIFF B 1 Offset(0,44)
		RIFF B 1 Offset(0,40)
		RIFF A 0 A_JumpIfInventory("MP40Seq",1,"Fire2")
		TNT1 A 0 A_GiveInventory("MP40Seq",1)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Fire2:
		RIFG A 1 Offset(0,36) A_TakeInventory("MP40Seq",1)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		RIFG A 1 Offset(0,35) A_PlaySound("reloads/mp40",5)
		RIFG A 1 Offset(0,38)
		RIFG A 1 Offset(0,44)
		RIFF B 1 Offset(0,52)
		RIFF B 1 Offset(-1,54)
		RIFF B 1 Offset(-2,56)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-4,58)
		RIFF B 1 Offset(-4,57)
		RIFF B 1 Offset(-3,54)
		RIFF B 1 Offset(-3,51)
		RIFF B 1 Offset(-3,53)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MPAmmo",1)
		TNT1 A 0 A_JumpIfInventory("MPAmmo",32,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MPClip",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		RIFF B 1 Offset(-3,57)
		RIFF B 1 Offset(-3,59)
		RIFF B 1 Offset(-3,63)
		RIFF B 1 Offset(-3,67)
		RIFF B 1 Offset(-3,65)
		RIFF B 1 Offset(-3,62)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-2,53)
		RIFF B 1 Offset(-2,49)
		RIFF B 1 Offset(-2,46)
		RIFF B 1 Offset(-2,45)
		RIFF B 1 Offset(-1,44)
		RIFF B 1 Offset(-1,46)
		RIFF B 1 Offset(-1,47)
		RIFF B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	Spawn:
		MP40 A -1
		Stop
	}
}

ACTOR MPClip : Ammo replaces ClipBox
{
	Tag "9x19mm"
	Inventory.Amount 64
	Inventory.MaxAmount 256
	Ammo.BackpackAmount 64
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "Picked up a box of 9mm ammo."
	Inventory.Icon "MCLPA0"
	States
	{
	Spawn:
		MCLP A -1
		Stop
	}
}

ACTOR MPAmmo : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 32
	Inventory.Icon "MP40A0"
}