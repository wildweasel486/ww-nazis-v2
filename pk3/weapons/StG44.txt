ACTOR STGClip : Ammo replaces Cell
{
	Tag "7.92x33mm"
	Inventory.Amount 15
	Inventory.MaxAmount 150
	Ammo.BackpackAmount 30
	Ammo.BackpackMaxAmount 270
	Inventory.PickupMessage "Picked up some Rifle ammo."
	Inventory.Icon "762AA0"
	ConversationID 183
	States
	{
	Spawn:
		762A A -1
		Stop
	}
}

ACTOR STGAmmo : Ammo
{
	Tag "7.92x33mm"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon STGIA0
}

Actor STG44 : NaziWeapon
{
	inventory.pickupmessage "You got the Sturmgewehr 44!"
	weapon.kickback 100
	Weapon.AmmoType "STGAmmo"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "STGClip"
	weapon.ammouse2 1
	weapon.ammogive2 20
	Scale 0.5
	Tag "Sturmgewehr 44"
	ConversationID 189
	States
	{
	Ready:
		STGG A 0 A_JumpIfInventory("STGAmmo",0,2)
		STGG A 0 A_JumpIfInventory("STGClip",1,2)
		STGG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
		STGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		STGG A 0 A_Lower
		STGG A 1 A_Lower
		Loop
	Select:
		STGG A 0 A_Raise
		STGG A 1 A_Raise
		Loop
	Fire:
		STGG A 0 A_JumpIfInventory("STGAmmo",1,1)
		Goto Altfire
		STGF A 0 A_GunFlash
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		STGF A 0 A_PlaySound("Weapons/STGFIRE",CHAN_WEAPON)
		STGF A 0 A_TakeInventory("STGAmmo",1)
		STGF A 1 bright A_FireCustomMissile("STGTracer",frandom(-2.5,2.5),0,0,0,0,frandom(-1.0,1.0))
		STGF B 1 bright Offset(0,38) A_SetPitch(pitch - 1.0)
		STGG A 1 Offset(0,37) A_AlertMonsters
		STGG A 1 Offset(0,35)
		STGG A 1 Offset(0,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Dryfire:
	Altfire:
	Reload:
		STGG A 1 Offset(0,35) A_PlaySound("reloads/stg",CHAN_ITEM)
		TNT1 A 0 A_Jump(160,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		STGG A 1 Offset(0,38)
		STGG A 1 Offset(0,44)
		STGG A 1 Offset(-2,46)
		STGG A 1 Offset(-4,52)
		STGG A 1 Offset(-7,56)
		STGG A 1 Offset(-10,57)
		STGG A 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("STGClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("STGAmmo",1)
		TNT1 A 0 A_JumpIfInventory("STGAmmo",30,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("STGClip",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		STGG A 1 Offset(-10,58)
		STGG A 1 Offset(-10,61)
		STGG A 1 Offset(-10,65)
		STGG A 1 Offset(-10,71)
		STGG A 1 Offset(-10,65)
		STGG A 1 Offset(-10,60)
		STGG A 1 Offset(-10,55)
		STGG A 1 Offset(-10,53)
		STGG A 1 Offset(-10,51)
		STGG A 1 Offset(-9,50)
		STGG A 1 Offset(-8,46)
		STGG A 1 Offset(-7,43)
		STGG A 1 Offset(-5,40)
		STGG A 1 Offset(-3,37)
		STGG A 1 Offset(-1,34)
		STGG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		STGI A -1
		Stop
	}
}