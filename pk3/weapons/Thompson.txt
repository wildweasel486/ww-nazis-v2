ACTOR ThompsonClip : Ammo
{
	Tag ".45ACP"
	Inventory.MaxAmount 20
	+IGNORESKILL
}

ACTOR 45ACPAmmo : Ammo
{
	Tag ".45ACP"
	Inventory.Amount 20
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 80
	Ammo.BackpackMaxAmount 450
	Scale 0.6
	Inventory.Icon "45AMA0"
	Inventory.PickupMessage "Picked up some .45ACP ammo."
	States
	{
	Spawn:
		45AM A -1
		Stop
	}
}

Actor Thompson : NaziWeapon
{
	inventory.pickupmessage "You got the Thompson M1A1!"
	weapon.kickback 100
	Weapon.AmmoType "ThompsonClip"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "45ACPAmmo"
	weapon.ammouse2 1
	weapon.ammogive2 100
	Scale 0.5
	Tag "Thompson M1A1"
	States
	{
	Ready:
		THOM A 0 A_JumpIfInventory("ThompsonClip",0,2)
		THOM A 0 A_JumpIfInventory("45ACPAmmo",1,2)
		THOM A 1 A_WeaponReady
		Loop
		THOM A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		THOM A 0 A_Lower
		THOM A 1 A_Lower
		Loop
	Select:
		THOM A 0 A_Raise
		THOM A 1 A_Raise
		Loop
	Fire:
		THOM A 0 A_JumpIfInventory("ThompsonClip",1,1)
		Goto Reload
		THOM B 0 A_GunFlash
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		THOM B 0 A_PlaySound("Thompson/fire",CHAN_WEAPON)
		THOM B 0 A_TakeInventory("ThompsonClip",1)
		THOM B 1 bright A_FireCustomMissile("ThompsonTracer",frandom(-1.5,1.5),0,0,0,0,frandom(-1.0,1.0))
		THOM C 1 bright Offset(0,38) A_SetPitch(pitch - 0.5)
		THOM A 1 Offset(0,34) A_AlertMonsters
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		THOM A 1 Offset(0,35) A_PlaySound("Thompson/reload",CHAN_ITEM)
		TNT1 A 0 A_Jump(160,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		THOM A 1 Offset(0,38)
		THOM A 1 Offset(0,44)
		THOM A 1 Offset(-2,46)
		THOM A 1 Offset(-4,52)
		THOM A 1 Offset(-7,56)
		THOM A 1 Offset(-10,57)
		THOM A 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("45ACPAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("ThompsonClip",1)
		TNT1 A 0 A_JumpIfInventory("ThompsonClip",20,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		THOM A 1 Offset(-10,58)
		THOM A 1 Offset(-10,61)
		THOM A 1 Offset(-10,65)
		THOM A 1 Offset(-10,71)
		THOM A 1 Offset(-10,65)
		THOM A 1 Offset(-10,60)
		THOM A 1 Offset(-10,55)
		THOM A 1 Offset(-10,53)
		THOM A 1 Offset(-10,51)
		THOM A 1 Offset(-9,50)
		THOM A 1 Offset(-8,46)
		THOM A 1 Offset(-7,43)
		THOM A 1 Offset(-5,40)
		THOM A 1 Offset(-3,37)
		THOM A 1 Offset(-1,34)
		THOM A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		THOM P -1
		Stop
	}
}

ACTOR ThompsonSpawner replaces Chaingun
{
	DropItem "Thompson"
	DropItem "45ACPAmmo"
	DropItem "45ACPAmmo"
	DropItem "45ACPAmmo"
	States
	{
	Spawn:
		TNT1 A 1 NODELAY A_NoBlocking
		Stop
	}
}