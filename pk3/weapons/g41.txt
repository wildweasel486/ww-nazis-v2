ACTOR ShellSpawner : RandomSpawner replaces Shell
{
	DropItem "NewShell"
	DropItem "MauserAmmoSmall"
	DropItem "MauserAmmoSmall"
}

ACTOR ShellSpawner2 : RandomSpawner replaces ShellBox
{
	DropItem "NewShellBox"
	DropItem "MauserAmmo"
	DropItem "MauserAmmo"
}

ACTOR MauserAmmo : Ammo
{
	Tag "7.92x57mm"
	Inventory.Amount 20
	Inventory.MaxAmount 40
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "M1GAA0"
	Inventory.PickupMessage "Picked up a box of 7.92mm Mauser ammo."
	ConversationID 180
	States
	{
	Spawn:
		M1GA A -1
		Stop
	}
}

ACTOR MauserAmmoSmall : MauserAmmo
{
	Inventory.Amount 5
	Inventory.PickupMessage "Picked up a 7.92mm stripper clip."
	Scale 0.3
	States
	{
	Spawn:
		M1GA B -1
		Stop
	}
}

ACTOR G41Clip : Ammo
{
	Tag "7.92x57mm"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon "M1GPA0"
}

ACTOR G41 : NaziWeapon
{
	Weapon.Kickback 100
	Weapon.AmmoType1 "G41Clip"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "MauserAmmo"
	Weapon.AmmoGive2 10
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the Gewehr 43!"
	Tag "Gewehr 43"
	+NOAUTOFIRE
	States
	{
	Spawn:
		M1GP A -1
		Stop
	Select:
		M1GG A 0 A_Raise
		M1GG A 1 A_Raise
		Loop
	Deselect:
		M1GG A 0 A_Lower
		M1GG A 1 A_Lower
		Loop
	Ready:
		M1GG A 0 A_JumpIfInventory("G41Clip",0,2)
		M1GG A 0 A_JumpIfInventory("MauserAmmo",1,2)
		M1GG A 1 A_WeaponReady
		Loop
		M1GG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		M1GG A 0 A_JumpIfInventory("G41Clip",1,1)
		Goto Reload
		M1GF A 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		M1GF A 0 A_GunFlash
		M1GF A 1 BRIGHT A_FireCustomMissile("RifleTracer")
		M1GF B 1 BRIGHT A_SetPitch (pitch - 1.4)
		M1GG CDEDC 1 A_AlertMonsters
		TNT1 A 0 A_CheckReload
		M1GG AAFFFA 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		TNT1 A 0 A_Jump(180,2)
		TNT1 A 0 A_PlaySound("*taunt", CHAN_VOICE)
		M1GG A 1 Offset(0,35) A_PlaySound("reloads/g43",5)
		M1GG A 1 Offset(-2,38)
		M1GG A 1 Offset(-4,44)
		M1GG A 1 Offset(-6,52)
		M1GG A 1 Offset(-7,57)
		M1GG C 1 Offset(-8,62)
		M1GG C 1 Offset(-9,67)
		M1GG C 1 Offset(-10,72)
		M1GG C 1 Offset(-11,77)
		M1GG C 1 Offset(-11,80)
		M1GG D 1 Offset(-12,82)
		M1GG D 1 Offset(-12,84)
		M1GG D 1 Offset(-13,86)
		M1GG E 1 Offset(-13,87)
		M1GG E 2 Offset(-14,88)
		M1GG E 3 Offset(-14,89)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MauserAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("G41Clip",1)
		TNT1 A 0 A_JumpIfInventory("G41Clip",10,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MauserAmmo",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		M1GG D 2 Offset(-10,96)
		M1GG D 1 Offset(-6,100)
		M1GG C 1 Offset(-7,90)
		M1GG C 1 Offset(-6,80)
		M1GG C 1 Offset(-4,70)
		M1GG A 1 Offset(-2,60)
		M1GG A 1 Offset(0,50)
		M1GG A 1 Offset(0,40)
		M1GG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}